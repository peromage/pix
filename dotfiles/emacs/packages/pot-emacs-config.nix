{ stdenvNoCC, runCommand, loadFiles ? [] }:

let
  loadFilesSourceStr = with builtins; concatStringsSep
    " "
    (map (f: assert readFileType f == "regular"; toString f) loadFiles);

  loadFilesBundle = runCommand "load-files-bundles" {} ''
    mkdir $out
    if [ -n "${loadFilesSourceStr}" ]; then
      cp ${loadFilesSourceStr} $out
    fi
  '';

in stdenvNoCC.mkDerivation {
  pname = "pot-emacs-config";
  version = "0.0.1";
  src = ../home-files/.emacs.d;
  sourceRoot = ".emacs.d";
  buildInputs = [ loadFilesBundle ];

  buildPhase = ''
    load_list=
    for f in ${loadFilesBundle}/*; do
      load_list="$load_list(load \"$f\")\n"
    done
    if [ -n "$load_list" ]; then
      sed -i "/;; Load custom/i;;; Generated by pot-emacs-config ;;;\n\n$load_list\n;;; End pot-emacs-config ;;;\n" init.el
    fi
  '';

  installPhase = ''
    mkdir -p $out/dot-emacs-d
    cp -a * $out/dot-emacs-d
  '';
}
