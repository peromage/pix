{ stdenvNoCC, runCommand, perl, loadFiles ? [] }:

let
  loadFilesSourceStr = with builtins; concatStringsSep
    " "
    (map (f: assert readFileType f == "regular"; toString f) loadFiles);

  loadFilesBundle = runCommand "load-files-bundles" {} ''
    TARGET="$out/.load-files"
    mkdir "$out"

    for f in ${loadFilesSourceStr}; do
      cp "$f" "$out"
      # Keep the passed-in order
      printf "(load \"$out/%s\")\n" "$(basename "$f")" >>"$TARGET"
    done
  '';

in stdenvNoCC.mkDerivation {
  pname = "pot-emacs-config";
  version = "0.0.1";
  src = ../home-files/.emacs.d;
  sourceRoot = ".emacs.d";
  dontPatchShebangs = true;
  buildInputs = [ loadFilesBundle ];
  nativeBuildIputs = [ perl ];

  buildPhase = ''
    SOURCE="${loadFilesBundle}/.load-files"
    if ! [ -f "$SOURCE" ]; then
      # Nothing to do
      return 0
    fi

    temp="$(mktemp)"

    while IFS= read -r line; do
      echo "$line"
      if [[ "$line" == *";; ANCHOR-PRE-CUSTOM-EL"* ]]; then
        printf ";;; Generated by pot-emacs-config ;;;\n";
        cat "$SOURCE"
        printf ";;; End pot-emacs-config ;;;\n";
      fi
    done <init.el >"$temp"

    cp -f "$temp" init.el
  '';

  installPhase = ''
    DIR="$out/etc/pot-emacs-config"
    mkdir -p "$DIR"
    cp -a * "$DIR"
  '';
}
