{ stdenvNoCC ,
  runCommand,
  # Extra .el files to be loaded in init.el
  load ? [],
  # Extra files/directories included with this config
  include ? []
}:

let
  loadFiles = with builtins; concatStringsSep
    " "
    (map (f: assert readFileType f == "regular"; toString f) load);

  loadBundlePackage = runCommand "load-bundle" {} ''
    DIR="$out/etc/pot-emacs-config-load-bundle"
    LOAD_EL="$DIR/.load.el"
    mkdir -p "$DIR"

    for f in ${loadFiles}; do
      cp "$f" "$DIR"
      # Keep the passed-in order
      printf "(load \"$DIR/%s\")\n" "$(basename "$f")" >>"$LOAD_EL"
    done
  '';

in stdenvNoCC.mkDerivation {
  pname = "pot-emacs-config";
  version = "0.0.1";
  src = ../home-files/.emacs.d;
  srcs = include;
  sourceRoot = ".emacs.d";
  dontPatchShebangs = true;
  buildInputs = [ loadBundlePackage ];

  unpackPhase = ''
    cp -r "$src" .emacs.d
    chmod u+w .emacs.d

    for s in $srcs; do
      cp -r "$s" "./.emacs.d/''${s#*-}"
    done
  '';

  buildPhase = ''
    SOURCE="${loadBundlePackage}/etc/pot-emacs-config-load-bundle/.load.el"
    if ! [ -f "$SOURCE" ]; then
      # Nothing to do
      return 0
    fi

    temp="$(mktemp)"

    while IFS= read -r line; do
      echo "$line"
      if [[ "$line" == *";; ANCHOR-PRE-CUSTOM-EL"* ]]; then
        printf ";;; Generated by pot-emacs-config ;;;\n";
        cat "$SOURCE"
        printf ";;; End pot-emacs-config ;;;\n";
      fi
    done <init.el >"$temp"

    cp -f "$temp" init.el
  '';

  installPhase = ''
    DIR="$out/etc/pot-emacs-config"
    mkdir -p "$DIR"
    cp -a * "$DIR"
  '';
}
